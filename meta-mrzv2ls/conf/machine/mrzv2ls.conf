#@TYPE: Machine
#@NAME: mrzv2ls machine
#@DESCRIPTION: ARIES SMARC MRZV2LS

SOC_FAMILY = "r9a07g054l"
require conf/machine/include/rzv2l-common.inc
require conf/machine/include/cip-core.inc

DEFAULTTUNE ?= "cortexa55"

TUNE_CONF ?= "conf/machine/include/tune-cortexa55.inc"
require ${TUNE_CONF}

PREFERRED_PROVIDER_virtual/kernel = "linux-renesas"

UBOOT_CONFIG ??= "mrzv2ls"
UBOOT_CONFIG[mrzv2ls] = "mrzv2ls_defconfig"

# flash writer
EXTRA_IMAGEDEPENDS += " flash-writer"

KERNEL_DEVICETREE = "aries/mrzv2ls.dtb"
KERNEL_IMAGETYPES = "Image.gz"

IMAGE_BOOT_FILES = " \
	bl2_bp-${MACHINE}.bin \
	fip-${MACHINE}.bin \
	bl2_bp-${MACHINE}.srec \
	fip-${MACHINE}.srec \
	Flash_Writer_SCIF_RZV2L_SMARC_PMIC_DDR4_2GB_1PCS.mot \
"

WIC_INPUT_DEPENDS = "firmware-pack:do_deploy flash-writer:do_deploy"

WKS_FILE = "rz-image-bootpart-mmc.wks"
IMAGE_FSTYPES += "wic.gz"

# Force remake wic image if its inputs were changed (WIC_INPUT_DEPENDS are defined in each machine conf)
# Note that environment variable WKS_FILE_DEPENDS can be used here, but it makes do_rootfs rerun as well
do_image_wic[depends] += "${WIC_INPUT_DEPENDS}"
